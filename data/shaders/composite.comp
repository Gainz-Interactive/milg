#version 460

layout(local_size_x = 16, local_size_y = 16) in;

layout(rgba8, set = 0, binding = 0) uniform readonly image2D in_image;
layout(rgba8, set = 0, binding = 1) uniform readonly image2D in_emissive;
layout(rgba16f, set = 0, binding = 2) uniform readonly image2D in_lightmap;

layout(rgba8, set = 0, binding = 3) uniform writeonly image2D out_image;

layout(push_constant) uniform PushConstants {
    float exposure;
} push_constants;

const mat3 ACES_input_mat = mat3(
        vec3(0.59719, 0.35458, 0.04823),
        vec3(0.07600, 0.90834, 0.01566),
        vec3(0.02840, 0.13383, 0.83777)
    );

const mat3 ACES_output_mat = mat3(
        vec3(1.60475, -0.53108, -0.07367),
        vec3(-0.10208, 1.10813, -0.00605),
        vec3(-0.00327, -0.07276, 1.07602)
    );

vec3 RRT_ODT_fit(vec3 v) {
    vec3 a = v * (v + 0.0245786f) - 0.000090537f;
    vec3 b = v * (0.983729f * v + 0.4329510f) + 0.238081f;

    return a / b;
}

vec3 ACES_fitted(vec3 color) {
    color = transpose(ACES_input_mat) * color;
    color = RRT_ODT_fit(color);
    color = transpose(ACES_output_mat) * color;
    color = clamp(color, vec3(0.0), vec3(1.0));

    return color;
}

void main() {
    ivec2 sample_pos = ivec2(gl_GlobalInvocationID.xy);
    vec2 uv = vec2(sample_pos + vec2(0.5)) / vec2(imageSize(in_image));
    ivec2 image_size = imageSize(in_image);

    vec4 scene = imageLoad(in_image, sample_pos);
    vec4 emissive = imageLoad(in_emissive, sample_pos);
    vec4 lightmap = imageLoad(in_lightmap, sample_pos);

    vec3 scene_color = mix(scene.rgb, emissive.rgb, emissive.a);
    scene_color = scene.rgb;

    vec3 color = scene_color * (lightmap.rgb + emissive.rgb);
    color = ACES_fitted(color * push_constants.exposure);
    color = pow(color, vec3(1.0 / 2.2));

    imageStore(out_image, sample_pos, vec4(color, 1.0));
}
